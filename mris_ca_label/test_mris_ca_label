#!/bin/tcsh -f

#
# test_mris_ca_label
#
# execute mris_ca_label and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2010/03/27 22:27:06 $
#    $Revision: 1.1 $
#
# Copyright (C) 2010,
# The General Hospital Corporation (Boston, MA). 
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mris_ca_label,v 1.1 2010/03/27 22:27:06 nicks Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRIS_CA_LABEL_TEST ) then
  echo "skipping test_mris_ca_label"
  exit 77
endif

setenv SUBJECTS_DIR $PWD

if ( -e testdata) then
  echo "rm -Rf testdata"
  rm -Rf testdata
endif

echo "gunzip -c testdata.tar.gz | tar xvf -"
gunzip -c testdata.tar.gz | tar xvf -

set cmd=(./mris_ca_label  -l testdata/label/lh.cortex.label \
    -aseg testdata/mri/aseg.mgz \
    -seed 1234 \
    testdata \
    lh \
    testdata/surf/lh.sphere.reg \
    ../distribution/average/lh.curvature.buckner40.filled.desikan_killiany.2010-03-25.gcs \
    testdata/label/lh.aparc.annot)
echo ""
echo $cmd
echo "Output is directed to file 'test_mris_ca_label.log'..."
$cmd >& test_mris_ca_label.log
if ($status != 0) then
  echo "test_mris_ca_label FAILED"
  exit 1
endif

set cmd=(./mris_ca_label  -l testdata/label/lh.cortex.label \
    -aseg testdata/mri/aseg.mgz \
    -seed 1234 \
    testdata \
    lh \
    testdata/surf/lh.sphere.reg \
    ../distribution/average/lh.destrieux.simple.2009-07-29.gcs \
    testdata/label/lh.aparc.a2009s.annot)
echo ""
echo $cmd
echo "Output is directed to file 'test_mris_ca_label.log'..."
$cmd >>& test_mris_ca_label.log
if ($status != 0) then
  echo "test_mris_ca_label FAILED"
  exit 1
endif



set TEST_FILES=( lh.aparc.annot lh.aparc.a2009s.annot )
foreach tst ($TEST_FILES)
  # now compare expected with observed data
  set REF  = testdata/label/reference-$tst
  set TST  = testdata/label/$tst
  set cmd=(diff $REF $TST);
  echo ""
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "test_mris_ca_label $cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end

#
# cleanup
#
cd $SUBJECTS_DIR
rm -Rf testdata

echo ""
echo "test_mris_ca_label passed all tests"
exit 0
