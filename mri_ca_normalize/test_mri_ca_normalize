#!/bin/tcsh -f

#
# test_mri_ca_normalize
#
# execute mri_ca_normalize and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2009/06/08 03:00:45 $
#    $Revision: 1.3 $
#
# Copyright (C) 2009,
# The General Hospital Corporation (Boston, MA). 
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_ca_normalize,v 1.3 2009/06/08 03:00:45 nicks Exp $'

umask 002

#
# run mri_ca_normalize using typical input args (and the same as those used
# to produce the expected results)
#

set cmd=(./mri_ca_normalize \
    -mask brainmask.mgz \
    -c ctrl_pts.mgz \
    nu.mgz \
    ../distribution/average/RB_all_2008-03-26.gca \
    talairach.lta \
    norm.mgz)
echo $cmd
$cmd
if ($status != 0) then
  echo "mri_ca_normalize FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
#
set TST_VOL=norm.mgz
set REF_VOL=reference_norm.mgz
if ("`uname -m`" == "i686") set REF_VOL=reference_i686_norm.mgz
set DIFF_VOL = $PWD/mri_diff-$TST_VOL
set MRIDIFFF = $PWD/mri_diff-$TST_VOL.txt
set THRESHOLD = 0
set cmd=(../mri_diff/mri_diff --debug --thresh $THRESHOLD \
      --log $MRIDIFFF $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

set TST_VOL=ctrl_pts.mgz
set REF_VOL=reference_ctrl_pts.mgz
set DIFF_VOL = $PWD/mri_diff-$TST_VOL
set MRIDIFFF = $PWD/mri_diff-$TST_VOL.txt
set THRESHOLD = 0
set cmd=(../mri_diff/mri_diff --debug --thresh $THRESHOLD \
      --log $MRIDIFFF $REF_VOL $TST_VOL --diff $DIFF_VOL);
echo $cmd
$cmd
set diff_status=$status
if ($diff_status != 0) then
  echo "$cmd FAILED (exit status=$diff_status)"
  exit 1
endif

echo ""
echo "test_mri_ca_normalize passed all tests"
exit 0
