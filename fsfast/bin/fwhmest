#!/bin/csh -f
############################################################
# Name:  fwhmest - front end for FSL's smoothest
############################################################

set inputargs = ($argv);
set VERSION = '$Id'
set PrintHelp = 0;

set InStem = ();
set PolyOrder = -1;
set xmatfile = ();
set extregstem = ();
set MaskStem = ();
set tmpdir = ();
set outfile = ();
set monly = 0;
set fmt = ();
set fmtext = ();
set MLF = ();
set cleanup = 1;

if($#argv == 0) goto usage_exit;
set n = `echo $argv | grep version | wc -l` 
if($n != 0) then
  echo $VERSION
  exit 0;
endif
set n = `echo $argv | grep help | wc -l` 
if($n != 0) then
  set PrintHelp = 1;
  goto usage_exit;
endif

goto parse_args;
parse_args_return:

goto check_params;
check_params_return:

## Get path for log-file ##
set LF = $InStem"_fwhm.log"
echo "Logfile is $LF"
rm -f $LF;
touch $LF;
echo ------------------------------------------ >> $LF
echo "fwhmest $VERSION" >> $LF
uname -a >> $LF
hostname >> $LF
date >> $LF
pwd >> $LF
echo $0 >> $LF
echo $inputargs >> $LF

if($#tmpdir == 0) then
  set indir = `dirname $InStem`;
  set tmpdir = $indir/fwhmest-tmp
endif

mkdir -p $tmpdir  |& tee -a $LF
if($status) exit 1;

set doffile = $tmpdir/dof.dat
rm -f $doffile

if($PolyOrder < 0 && $#xmatfile == 0 && $#extregstem == 0) goto smoothest;

## Set path for matlab file ##
if(! $monly)  set MLF = "/tmp/fwhmest_$$.m"
rm -f $MLF

set outstem = $tmpdir/f;

  #-----------------------------------------------------#
tee $MLF > /dev/null <<EOF
  instem  = '$InStem';
  outstem = '$outstem';
  order   = [$PolyOrder];
  doffile = '$doffile';
  xmatfile = '$xmatfile';
  extregstem = '$extregstem';
  fmtext = '$fmtext';

  %[f mristruct] = fast_ldbslice(instem);
  fspec = sprintf('%s%s',instem,fmtext);
  f = MRIread(fspec);
  if(isempty(f))
    fprintf('ERROR: loading %s\n',fspec);
    return;
  end

  szf = size(f.vol);
  nv = prod(szf(1:3));
  nf = szf(4);
  f.vol = reshape(f.vol,[nv nf])';

  X = [];

  if(~isempty(xmatfile))
    XX = load(xmatfile);
    if(isempty(XX))
      fprintf('ERROR: loading %s\n',xmatfile);
      return; quit;
    end
    X = [X XX.Xfinal];
  end

  if(~isempty(extregstem))
    extreg = fmri_ldbvolume(extregstem);
    if(isempty(extreg))
      fprintf('ERROR: could not load %s\n',extregstem);
      return;
    end
    if(size(extreg,3)~=1) extreg = squeeze(extreg)'; %'
    else                  extreg = squeeze(extreg);
    end
    % normalize
    extreg = extreg - repmat(mean(extreg), [nf 1]);
    extreg = extreg./repmat(std(extreg), [nf 1]);
    X = [X extreg];
  end

  if(order > -1)
    Xp = fast_polytrendmtx(1,nf,1,order);
    X = [X Xp];
  end

  if(~isempty(X))
    R = eye(nf) - X*inv(X'*X)*X';
    f.vol = R*f.vol;
    dof = size(X,1) - size(X,2);
  else
    dof = nf;
  end

  f.vol = reshape(f.vol',szf);

  fspec = sprintf('%s%s',outstem,fmtext);
  MRIwrite(f,fspec);

  %fast_svbslice(f,outstem,-1,'',mristruct);

  fp = fopen(doffile,'w');
  fprintf(fp,'%g\n',dof);
  fclose(fp);

  return; quit;
EOF
  #-----------------------------------------------------#

cat $MLF | matlab -nospash -display iconic |& tee -a $LF
if($fmt == bvolume) then
  mri_convert $outstem"_000.bfloat" $tmpdir/f.img
else
  mri_convert $outstem$fmtext $tmpdir/f.img
endif

smoothest:

if($PolyOrder < 0 && $#xmatfile == 0 && $#extregstem == 0) then
  #set dof = `getnframes $InStem`;
  set dof = `mri_info --nframes $InStem$fmtext `;
  echo $dof > $doffile
  if($fmt == bvolume) then
    set bext = `getbext $InStem`;
    mri_convert $InStem"_000.$bext" $tmpdir/f.img
  else
    mri_convert $InStem$fmtext $tmpdir/f.img
  endif
endif

if($fmt == bvolume) then
  set bext = `getbext $MaskStem`;
  mri_convert $MaskStem"_000.$bext" $tmpdir/mask.img
else
  mri_convert $MaskStem$fmtext $tmpdir/mask.img
endif

set dof = `cat $doffile`;
set cmd = (smoothest -V -d $dof -m $tmpdir/mask.img -r $tmpdir/f.img)
echo $cmd |& tee $LF
$cmd > $outfile

cat $outfile

if($cleanup) rm -r $tmpdir

echo " " | tee -a $LF
echo " " | tee -a $LF
date | tee -a $LF
echo "fwhmest: finished" | tee -a $LF

exit 0;
############################################################

############--------------##################
parse_args:

set cmdline = ($argv);
while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "--i":
      if ( $#argv == 0) goto arg1err;
      set InStem = $argv[1]; shift;
      breaksw

    case "--o":
      if ( $#argv == 0) goto arg1err;
      set outfile = $argv[1]; shift;
      breaksw

    case "--polyfit":
      if ( $#argv == 0) goto arg1err;
      set PolyOrder = $argv[1]; shift;
      breaksw

    case "--X":
      if ( $#argv == 0) goto arg1err;
      set xmatfile = $argv[1]; shift;
      breaksw

    case "--extreg":
      if ( $#argv == 0) goto arg1err;
      set extregstem = $argv[1]; shift;
      breaksw

    case "--mask":
      if ( $#argv == 0) goto arg1err;
      set MaskStem = $argv[1]; shift;
      breaksw

    case "--umask":
      if ( $#argv == 0) goto arg1err;
      umask $1; shift;
      breaksw

    case "--fmt":
      if ( $#argv == 0) goto arg1err;
      set fmt = $argv[1]; shift;
      breaksw

    case "--nocleanup":
      set cleanup = 0;
      breaksw

    case "--debug":
      set verbose = 1;
      set echo = 1;
      breaksw


    default:
      echo ERROR: Flag $flag unrecognized.
      echo $cmdline
      exit 1
      breaksw
  endsw

end
goto parse_args_return;
############--------------##################

############--------------##################
check_params:

  if($#InStem == 0) then
     echo "ERROR: must specify input stem";
     exit 1
  endif
  if($#MaskStem == 0) then
     echo "ERROR: must specify mask stem";
     exit 1
  endif
  if($#outfile == 0) set outfile = $InStem.fwhm.dat

  if($#fmt == 0) then
    echo "Autodetecting format"
    if(-e $InStem.mgh) then
      set fmt = "mgh";
    else if(-e $InStem.mgz) then
      set fmt = "mgz";
    else if(-e $InStem.bhdr) then
      set fmt = "bhdr";
    else
      echo "ERROR: cannot find $Instem"
      exit 1;
    endif
    echo "Detected $fmt format"
  endif

  if("$fmt" == "bvolume") then
    set fmtext = ();
  else if("$fmt" == "mgh") then
    set fmtext = ".mgh";
  else if("$fmt" == "mgz") then
    set fmtext = ".mgz";
  else
    echo "ERROR: format $fmt unrecognized"
    exit 1;
  endif

goto check_params_return;
############--------------##################


############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

#--------------------------------------------------------------------#
usage_exit:
  echo "fwhmest"
  echo "  --i instem "
  echo "  --mask maskstem"
  echo ""
  echo "  --fmt format : bvolume, mgh, or mgz"
  echo "  --polyfit N : default is 0"
  echo "  --X xmatfile"
  echo "  --extreg extregstem"
  echo "  --o outfile : default is instem.fwhm.dat"
  echo "   -umask umask          : set unix file permission mask"
  echo "   -version              : print version and exit"
  if($PrintHelp) \
  cat $0 | awk 'BEGIN{prt=0}{if(prt) print $0; if($1 == "BEGINHELP") prt = 1 }'

exit 1;

#---- Everything below here is printed out as part of help -----#
BEGINHELP

Frontend for FSLs smoothest
