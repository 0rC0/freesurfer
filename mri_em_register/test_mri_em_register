#!/bin/tcsh -f

#
# test_mri_em_register
#
# execute mri_em_register and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2014/11/03 18:59:33 $
#    $Revision: 1.1 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_em_register,v 1.1 2014/11/03 18:59:33 nicks Exp $'

umask 002

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_em_register using typical input file formats,
# first with one thread then with 8 threads.
#

cd testdata

setenv OMP_NUM_THREADS 1
set cmd1=(../mri_em_register \
    -uns 3 \
    -mask brainmask.mgz \
    nu.mgz \
    ../../distribution/average/RB_all_2014-08-21.gca \
    talairach.1cpu.lta)
echo ""
echo $cmd1
echo $cmd1 > CMDF1

setenv OMP_NUM_THREADS 8
set cmd2=(../mri_em_register \
    -uns 3 \
    -mask brainmask.mgz \
    nu.mgz \
    ../../distribution/average/RB_all_2014-08-21.gca \
    talairach.8cpu.lta)
echo ""
echo $cmd2
echo $cmd2 >> CMDF2

# run both commands in parallel
../../scripts/reconbatchjobs test.log CMDF1 CMDF2
if ($status != 0) then
  echo "mri_em_register FAILED"
  exit 1
endif

echo ""
echo ""
echo ""

#
# compare expected results with actual (produced) results
#

set TEST_FILES=( talairach.1cpu.lta talairach.8cpu.lta )
foreach tstf ($TEST_FILES)
  set cmd=(../../mri_robust_register/lta_diff talairach.lta $tstf);
  echo ""
  echo $cmd
  $cmd
  set diff_status=$status
  if ($diff_status != 0) then
    echo "$cmd FAILED (exit status=$diff_status)"
    exit 1
  endif
end

#
# cleanup
#
cd ..
rm -Rf testdata

echo ""
echo "test_mri_em_register passed all tests"
exit 0
