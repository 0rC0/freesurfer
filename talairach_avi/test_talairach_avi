#!/bin/tcsh -f

#
# test_talairach_avi
#
# execute talairach_avi and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2011/03/01 15:31:45 $
#    $Revision: 1.6 $
#
# Copyright (C) 2009,
# The General Hospital Corporation (Boston, MA). 
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_talairach_avi,v 1.6 2011/03/01 15:31:45 nicks Exp $'

umask 002

set path=(. ../mri_convert ../tkregister2/cmdl $path)
rehash

# fake-out an installation:
setenv SUBJECTS_DIR .
setenv FREESURFER_HOME .
rm -f average
ln -s . average
rm -f mni305.cor.mgz
ln -s ../distribution/average/mni305.cor.mgz
rm -Rf tktools
mkdir -p tktools
cp ../scripts/tkregister2.tcl tktools/

set cmd=(talairach_avi --i nu.mgz --xfm tal.xfm)
echo "test_talairach_avi: output logged to test_talairach_avi.log..."
$cmd >& test_talairach_avi.log
if ($status != 0) then
  echo "talairach_avi FAILED"
  exit 1
endif

#
# compare expected results with actual (produced) results
# use lta_diff, since there will be small variations between platforms
#
set cmd=(../mri_robust_register/lta_diff tal.xfm reference-tal.xfm)
echo ""
echo $cmd
set diffVal=(`$cmd`)  
if ($status) then
  echo "$cmd FAILED"
  exit 1
endif
echo "rigid transform distance = $diffVal"
set diffBool=(`echo "$diffVal > 0.015" | bc`)
if ($diffBool) then
  echo "$cmd FAILED: rigid transform distance = $diffVal > 0.015"
  exit 1
endif

echo ""
echo "test_talairach_avi passed tests"
exit 0
