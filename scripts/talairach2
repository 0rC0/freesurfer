#!/bin/csh -f
# talairach2
#
# USAGE: talairach2 subjectid <mgzflag>
#  subjectid - name of the subject
#  mgzflag - if anything is present, then it will assume
#   that the orig volume is stored as orig.mgz in the mri
#   directory (ie, the orig dir does not exist and there
#   is no COR volume).
#
########################################################

set VERSION = '$Id: talairach2,v 1.6 2004/07/15 21:54:06 greve Exp $';

set MGZ = ();

# Look for version #
set n = `echo $argv | grep version | wc -l` 
if($n != 0) then
  echo $VERSION
  ## gather version info used by this script
  mri_convert --version
  set prog = `which mritotal`
  strings $prog | grep mritotal.in
  exit 0;
endif

if($#argv < 1 || $#argv > 2) then
  echo "USAGE: talairach2 subjectid <mgzflag>"
  exit 1;
endif

set subject = $argv[1]; shift;
if($#argv != 0) set MGZ = ".mgz"

set base = $SUBJECTS_DIR/$subject/mri

if(! -e $base) then
  echo "ERROR cannot find $base"
  exit 1;
endif

# model=/homes/nmrnew/home/inverse/local/lib/model/average_305.mnc
# dir=`pwd`

mkdir -p $base/transforms
set transform = $base/transforms/talairach.xfm
pushd $base > /dev/null
echo "INFO: registering $subject"
date 

echo "--------------------------------------------"
set cmd = (mri_convert orig$MGZ orig.mnc)
echo $cmd
$cmd
if($status) then
  echo "ERROR: mri_convert failed"
  exit 1;
endif

if(! -e orig.mnc) then
  echo "ERROR: cannot find orig.mnc"
  exit 1;
endif

echo "--------------------------------------------"
set cmd = (mritotal -verbose -debug -clobber -protocol icbm \
           orig.mnc transforms/talairach.xfm)
echo $cmd
$cmd > ../scripts/mritotal.log
set st = $status
rm orig.mnc
if($st) then
  echo "ERROR: mritotal failed, see scripts/mritotal.log"
  exit 1;
endif

echo "--------------------------------------------"
set cmd = (mri_add_xform_to_header $transform $base/orig$MGZ $base/orig$MGZ)
echo $cmd
$cmd
if($status) then
  echo "ERROR: mri_add_xform_to_header failed"
  exit 1;
endif

popd > /dev/null

date

exit 0
