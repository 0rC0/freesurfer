#! /bin/tcsh -ef

#
# petsc_setup
#
# Setup PETSC for apps using the PETSC package included with Freesurfer
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: nicks $
#    $Date: 2007/06/18 15:25:43 $
#    $Revision: 1.1 $
#
# Copyright (C) 2002-2007,
# The General Hospital Corporation (Boston, MA).
# All rights reserved.
#
# Distribution, usage and copying of this software is covered under the
# terms found in the License Agreement file named 'COPYING' found in the
# FreeSurfer source code root directory, and duplicated here:
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferOpenSourceLicense
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
# Bug reports: analysis-bugs@nmr.mgh.harvard.edu
#

### --  use NMR Center PETSC package -- ####

if ( $?FREESURFER_HOME ) then
  if ( -e $FREESURFER_HOME/lib/petsc ) then
    set FS_PETSC_HOME=$FREESURFER_HOME/lib/petsc
  else if ( -e /usr/pubsw/packages/petsc/current ) then
    set FS_PETSC_HOME=/usr/pubsw/packages/petsc/current
  endif
else if ( -e /usr/pubsw/packages/petsc/current ) then
  set FS_PETSC_HOME=/usr/pubsw/packages/petsc/current
endif

if ( $?FS_PETSC_HOME ) then

  if ( -e $FS_PETSC_HOME/bin ) then
    setenv PATH "$FS_PETSC_HOME/bin":"$PATH"
  endif

  if ( -e $FS_PETSC_HOME/lib ) then

    setenv PETSCLIBPATH $FS_PETSC_HOME/lib

    if(! $?LD_LIBRARY_PATH ) then
      setenv LD_LIBRARY_PATH $PETSCLIBPATH
    else
      setenv LD_LIBRARY_PATH "$PETSCLIBPATH":"$LD_LIBRARY_PATH"
    endif
    if(! $?DYLD_LIBRARY_PATH ) then
      setenv DYLD_LIBRARY_PATH $PETSCLIBPATH
    else
      setenv DYLD_LIBRARY_PATH "$PETSCLIBPATH":"$DYLD_LIBRARY_PATH"
    endif

    # if petsc python wrappers were built...
    if ( -e $PETSCLIBPATH/python2.4/site-packages) then
      setenv PETSCPYPATH $PETSCLIBPATH/python2.4/site-packages
    else if ( -e $PETSCLIBPATH/python2.3/site-packages) then
      setenv PETSCPYPATH $PETSCLIBPATH/python2.3/site-packages
    else if ( -e $PETSCLIBPATH/python2.2/site-packages) then
      setenv PETSCPYPATH $PETSCLIBPATH/python2.2/site-packages
    endif
    if ( $?PETSCPYPATH ) then
      if(! $?PYTHONPATH ) then
        setenv PYTHONPATH $PETSCPYPATH 
      else
        setenv PYTHONPATH "$PETSCPYPATH":"$PYTHONPATH"
      endif
    endif

  endif

  # default: assume platform has PETSC libs installed somewhere
endif
